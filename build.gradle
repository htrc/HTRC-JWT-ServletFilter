buildscript {
    dependencies {
        classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
    }
    repositories {
        maven {
            url "https://nexus.htrc.illinois.edu/content/groups/public"
        }
        mavenLocal()
        mavenCentral()
    }
}

plugins {
    id 'com.palantir.git-version' version '0.8.0'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'maven-publish-auth'

def details = versionDetails()

ext.calculateVersion = { ->
    def gitVer = "${gitVersion()}"
    def gitVerNoDirty = gitVer.replaceAll('\\.dirty$', '')

    // append -SNAPSHOT to anything other than builds off of 'master' branch (except if in detached HEAD mode)
    if (details.branchName) {
        if (details.branchName != 'master') {
            gitVerNoDirty = gitVerNoDirty + "-SNAPSHOT"
        }
    }

    return gitVerNoDirty
}

group = 'edu.indiana.d2i.htrc'
version = calculateVersion()

ext {
    artifactRepoBase = "https://nexus.htrc.illinois.edu/content/repositories"
    destRepo = "${version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}"
    repoName = "htrc.nexus.${destRepo}"
    repoUrl = "${artifactRepoBase}/${destRepo}"
}

repositories {
    mavenLocal()
    maven {
        url "https://nexus.htrc.illinois.edu/content/groups/public"
    }
}

dependencies { compile 'javax.servlet:javax.servlet-api:3.1.0'
    compile 'com.auth0:java-jwt:3.1.0'
    compile 'commons-logging:commons-logging:1.1.1'
    compile 'com.typesafe:config:1.3.1'
    compile 'org.mockito:mockito-all:1.10.19'
    testCompile 'junit:junit:4.12'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name "${repoName}"
            url "${repoUrl}"
        }
    }
}
